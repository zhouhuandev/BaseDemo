if (!isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply from: "../base.build.gradle"

android {

    signingConfigs {
        release {
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASS
            storeFile file(KEY_PATH)
            storePassword KAY_PASS
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    defaultConfig {
        if (!isModule.toBoolean()) {
            applicationId "com.hzsoft.basedemo"
        }
        multiDexEnabled true

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        manifestPlaceholders = [
                PACKAGE_NAME           : applicationId == null ? "" : applicationId,
                JPUSH_PKGNAME          : applicationId == null ? "" : applicationId,
                JPUSH_APPKEY           : "", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL          : "developer-default", //暂时填写默认值即可.

                BAIDU_SPEECH_APP_ID    : "",//百度语音技术APPID
                BAIDU_SPEECH_API_KEY   : "",//百度语音技术APPKEY
                BAIDU_SPEECH_SECRET_KEY: "",//百度语音技术SECRET_KEY
                BAIDU_LBSAPI_API_KEY   : "",//百度地图KEY
                BAIDU_TUISONG_API_KEY  : "",//百度推送
                GAODE_APIKEY           : "",//高德APPKEY

                EASEMOB_APPKEY         : "#basedemo",//环信APPKEY

                UMENG_APPKEY           : "",//友盟APPKEY
                UMENG_CHANNEL          : "",//友盟
        ]
        signingConfig signingConfigs.release
    }

    if (!isModule.toBoolean()) {
        //打包改名
        android.applicationVariants.all { variant ->
            def appName = rootProject.name
            def buildType = variant.buildType.name
            def outputTime = new Date().format("yyyy_MMdd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            variant.outputs.each { output ->
                if ("release" == buildType) {
                    if (output.outputFileName.endsWith('.apk')) {
                        output.outputFileName = "${versionName}\\${appName}_${versionCode}_V${versionName}_${outputTime}.apk"
                    }
                } else {
                    if (output.outputFileName.endsWith('.apk')) {
                        output.outputFileName = "${appName}_${versionCode}_V${versionName}_debug.apk"
                    }
                }
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {

    implementation project(":lib_base_module")
    if (!isModule.toBoolean()) {
        implementation project(':module_main')
        implementation project(':module_home')
        implementation project(':module_me')
    }
}
